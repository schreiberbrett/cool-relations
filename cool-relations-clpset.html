#|
<meta name="viewport" content="width=device-width, initial-scale=1" />
<style>
body {
  margin:0 auto; max-width: 700px;
  padding: 10px;
  font-family: system-ui;
}

img {
  max-width: 100%;
}
</style>

<h1>
Cool relations for
<a href="https://github.com/namin/clpset-miniKanren/">
clpset-miniKanren
</a>
</h1>
<p>
See also:

<ul>
  <li>
    <a href="cool-relations.html">
      cool-relations.html
    </a>
  </li>

  <li>
    <a href="cool-relations-faster-minikanren.html">
      cool-relations-faster-minikanren.html
    </a>
  </li>
</ul>
</p>

<pre>
|#
(load "../clpset-miniKanren/mk.scm")

(define (3coloro graph red green blue)
  (conde
    ((== graph empty-set))
    ((fresh (u v rest)
       (== graph (set rest (set empty-set u v)))
       (conde
         ((ino u red)   (ino v green))
         ((ino u red)   (ino v blue))
         ((ino u green) (ino v red))
         ((ino u green) (ino v blue))
         ((ino u blue)  (ino v red))
         ((ino u blue)  (ino v green)))
       (3coloro rest red green blue)))))

(define (3coloro/1 graph red green blue)
  (disjo red green)
  (disjo red blue)
  (disjo green blue)
  (3coloro graph red green blue))

(define (3coloro/2 graph red green blue)
  (3coloro graph red green blue)
  (disjo red green)
  (disjo red blue)
  (disjo green blue))

(define triangle
  (set
    empty-set
    (set empty-set 0 1)
    (set empty-set 0 2)
    (set empty-set 1 2)))

(define diamond
  (set
    empty-set
    (set empty-set 0 1)
    (set empty-set 0 2)
    (set empty-set 1 2)
    (set empty-set 1 3)
    (set empty-set 2 3)))

(define k4
  (set
    empty-set
    (set empty-set 0 1)
    (set empty-set 0 2)
    (set empty-set 1 2)
    (set empty-set 1 3)
    (set empty-set 2 3)
    (set empty-set 0 3)))

(define (3coloro/enum graph red green blue)
  (conde
    ((== graph empty-set))
    ((fresh (u v rest)
       (== graph (set rest (set empty-set u v)))
       (conde
         (( ino u red) (!ino u green) (!ino u blue)
          (!ino v red) ( ino v green) (!ino v blue))
         (( ino u red) (!ino u green) (!ino u blue)
          (!ino v red) (!ino v green) ( ino v blue))
         ((!ino u red) ( ino u green) (!ino u blue)
          ( ino v red) (!ino v green) (!ino v blue))
         ((!ino u red) ( ino u green) (!ino u blue)
          (!ino v red) (!ino v green) ( ino v blue))
         ((!ino u red) (!ino u green) ( ino u blue)
          ( ino v red) (!ino v green) (!ino v blue))
         ((!ino u red) (!ino u green) ( ino u blue)
          (!ino v red) ( ino v green) (!ino v blue)))
       (3coloro/enum rest red green blue)))))

(define (try graph)
  (run 1 (q) (fresh (x y z) (== q `(,x ,y ,z))
    (3coloro/enum graph x y z))))
#|
</pre>



|#
