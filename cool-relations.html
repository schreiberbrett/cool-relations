#|
<meta name="viewport" content="width=device-width, initial-scale=1" />
<style>
body {
  margin:0 auto; max-width: 700px;
  padding: 10px;
  font-family: system-ui;
}

img {
  max-width: 100%;
}
</style>

<h1>Cool relations</h1>

<h2 id="list-relations">List relations</h2>
<p>
Lists in miniKanren follow the tradition of recursively-defined linked lists
from functional and logic programming. Often this document will follow a
convention of using <code>a</code> and <code>d</code> as logic variables to
describe the head and tail of a non-empty list. This is in the tradition of the
host language functions <code>car</code> and <code>cdr</code>.
</p>

<p>
Here is <code>proper-listo</code> from The Reasoned Schemer, 2nd edition:
</p>

<pre id="proper-listo">
|#
(defrel (proper-listo l)
  (conde
    ((== l '()))
    ((fresh (a d)
       (== l `(,a . ,d))
       (proper-listo d)))))
#|
</pre>

<p>
There are certain miniKanren relations which describe types and data
structures. The relations should not be used directly, because they they can
ground variables too soon. Instead, use these as a pattern match for each
clause.
</p>

<p>
This technique is related to the following logical rule:
</p>
<pre>
  ((p ∧ a)     ∨ (¬p ∧ b))
∧ ((p ∧ x)     ∨ (¬p ∧ y))
-----------------------
  ( p ∧ a ∧ x) ∨ (¬p ∧ b ∧ y)
</pre>

<p>
A relation for detecting if a list is odd or even length is sometimes useful,
especially if you can ground the length's parity (describing an infinite set of
lists) before you commit to an a particular length.
</p>

<pre>
|#
(defrel (length-parityo l p)
  (conde 
    ((== l '()) (== p 'even))
    ((fresh (a d rec)
       (== l `(,a . ,d))
       (conde
         ((== p 'odd)  (== rec 'even))
         ((== p 'even) (== rec 'odd)))
       (length-parityo d rec)))))
#|
</pre>

<h3 id="appendo">Trusty <code>appendo</code></h3>

<p>
The classic relation over lists is <code>appendo</code>, which constrains three
lists, <code>l</code>, <code>r</code>, and <code>l++r</code> such that
<code>l++r</code> is the concatenation of <code>l</code> and <code>r</code>.
</p>

<pre>
|#
(defrel (appendo l r l++r)
  (conde
    ((== l '()) (== l++r r))
    ((fresh (a d d++r)
       (== l `(,a . ,d))
       (== l++r `(,a . ,d++r))
       (appendo d r d++r)))))
#|
</pre>

<p>
For an introduction to <code>appendo</code>, watch this clip at 34:12 of Will
Byrd presenting a tutorial at the 2023 miniKanren workshop:
<a href="https://youtu.be/e_yc9YaLNDE?t=2052">
https://youtu.be/e_yc9YaLNDE?t=2052</a>.
There are many things to appreciate about this relation. As Will mentions, it
is very similar to a recursive definition of the append <i>function</i> over
cons lists from functional programming, and the relational definition of
<code>appendo</code> can be derived from regular transformations over the
functional definition of <code>append</code>.
</p>

<p>
Another interesting property of this relation is that when <code>l++r</code> is
known, then all possible values for <code>l</code> and <code>r</code> can be
enumerated with <code>run*</code>. Being able to "run backwards" using
<code>run*</code> can be difficult to achieve.
</p>

<p>
Look what happens when all arguments remain fresh:
</p>

<pre>
> (run 5 (l r l++r) (appendo l r l++r))
((() _0 _0)
  ((_0) _1 (_0 . _1))
  ((_0 _1) _2 (_0 _1 . _2))
  ((_0 _1 _2) _3 (_0 _1 _2 . _3))
  ((_0 _1 _2 _3) _4 (_0 _1 _2 _3 . _4)))
</pre>

<p>
What we see here are the first 5 elements of an infinite enumeration of
triples. Each successive result has <code>l</code> length instantiated to the
next length, while <code>r</code> remains fresh, and <code>l++r</code> is
constrained to being a list which has <code>l</code> as a prefix and
<code>r</code> as a tail, exactly the definition of appending. The fact that
<code>l</code> is length-instantiated and <code>r</code> remains fresh is a
consequence of using cons lists. It is not easy to create an append relation
which length-instantiates <code>r</code> and leaves <code>l</code> fresh due to
the representation of lists in the host language.
</p>

<p>
These two facts: that <code>appendo</code> length-instantiates <code>l</code>,
and that <code>r</code> remains fresh when <code>l++r</code> is fresh, can be
determined statically; they can be known just by looking at the code.
Within the <code>appendo</code> relation, there is actually a definition of
<code>proper-listo</code>: <code>l</code> is either the empty list, or it is
the pair <code>`(,a . ,d)</code> with the recursive promise that <code>d</code>
is itself a proper list. It is easier to see the fact about <code>r</code>. The
only time <code>r</code> is mentioned in the <code>appendo</code> code is when
it is unified with <code>l++r</code>.
</p>

<p>
This static analysis of miniKanren relations is a promising area of research.
It would be very useful, say, in an IDE, to automatically know some of the
consequences of calling a relation on certain arguments.
</p>

<p>
One other thing to note is that even though <code>r</code> is free, it really
represents a proper list, even though a call to something like
<code>(appendo '(a b c) 'foo '(a b c . foo))</code> will succeed. In miniKanren
implementations embedded in Scheme, like TRS2e miniKanren and
faster-miniKanren, the programmer is responsible for maintaining this type
constraint. But there are implementations that can enforce this constraint at
compile-time, like
<a href="https://github.com/SnejUgal/typedKanren">typedKanren</a>.
</p>

<p>
One final observation: This <code>appendo</code> definition benefits from the
ability to write variable names in Scheme which can contain special symbols.
Here we rely on convention to help show that <code>l++r</code> is the result of
appending <code>l</code> to <code>r</code>, and similarly that
<code>d++r</code> is the result of appending <code>d</code> to <code>r</code>.
It can be annoying to have to name all the intermediate results in logic
programming, but at least we can take advantage of Scheme's flexible variable
naming to hint at the relationships between logic variables--it becomes clear
that there must be a call to <code>(appendo d r d++r)</code> somewhere in the
relation definition.
</p>

<h2 id="arithmetic-relations">Arithmetic relations</h2>

<h3 id="peano-numerals">Peano numerals</h3>

<p>
Peano numerals are represented as a list of repetitions of the symbol
<code>s</code>. For example, three is <code>'(s s s)</code>, five is
<code>'(s s s s s)</code>, and zero is the empty list, <code>'()</code>.
</p>

Like <a href="#proper-listo">proper-listo</a>, there is a relation that fully
grounds a Peano numeral, which should be used only as a recursion template for
other relations over Peano numerals.

<pre id="peanoo">
|#
(defrel (peanoo n)
  (conde
    ((== n '()))
    ((fresh (n-1)
       (== n `(s . ,n-1))
       (peanoo n-1)))))
#|
</pre>



<p>
There is a little-endian binary representation of natural numerals hereafter
known as Oleg numerals. Below is the analogous "grounding" relation.
</p>

<pre>
|#
(defrel (olego n)
  (conde
    ((== n '()))
    ((fresh (a d)
       (== n `(,a . ,d))
       (conde
         ((== a 0) (poso d))
         ((== a 1)))
       (olego d)))))
#|
</pre>

See
<a href="https://okmij.org/ftp/Prolog/Arithm/arithm.pdf">
Pure, Declarative, and Constructive Arithmetic Relations by Kiselyov, et al.
</a>
for further details.

<h2 id="sets-of-naturals">Sets of Oleg numerals</h2>
<p>
Here's an idea on how to represent sets of natural numbers in miniKanren.
</p>

<p>
I want to start with the simplest thing I can think of: testing a subset of the
naturals for membership. I'm still thinking about how to implement testing for
non-membership.
</p>

<p>
If you were to ask me which subset of the naturals contains 4, and 0, and 3,
I'd say {0 ... 3, 4, ... }.
</p>

<img src="img/relational-natset.svg">

<pre id="elemo">
|#
(defrel (elemo n s)
  (fresh (l m r)
    (== s `(,l ,m ,r))
    (conde
      ((== n '()) (== m #t))
      ((fresh (a d rec)
         (== n `(,a . ,d))
         (conde
           ((== a 0) (poso d) (== rec l))
           ((== a 1) (== rec r)))
         (elemo d rec))))))
#|
</pre>

<pre>
> (run* (q) (elemo '(0 0 1) q)
            (elemo '() q)
            (elemo '(1 1) q))
'((((_.0 _.1 (_.2 #t _.3)) _.4 _.5) #t (_.6 _.7 (_.8 #t _.9))))
</pre>

<p>
Raffi Sanna helped me figure out the code for its complement:
</p>

<pre id="not-elemo">
(defrel (not-elemo n s)
  (fresh (l r)
    (conde
      ((== s '()))
      ((== n '()) (== s `(,l #f ,r)))
      ((fresh (val b rec)
         (== s `(,val ,l ,r))
         (conde
           ((== n `(0 . ,b)) (poso b) (== rec l))
           ((== n `(1 . ,b)) (== rec r)))
         (not-elemo b rec))))))
</pre>

<h3 id="divisibility-by-three">Divisibility by three</h3>


<pre>
|#
(defrel (even-odd-helpero n diff)
  (conde
    [(== n '()) (== diff 0)]
    [(== n '(1)) (== diff 2)]
    [(== n '(0 1)) (== diff 1)]
    [(== n '(1 1)) (== diff 0)]
    [(fresh (a ad dd new-diff)
       (== n `(,a ,ad . ,dd))
       (poso dd)
       (conde
         [(== a ad) (== diff new-diff)]
         [(== `(,a ,ad) '(0 1)) (+1mod3o new-diff diff)]
         [(== `(,a ,ad) '(1 0)) (+1mod3o diff new-diff)])
       (even-odd-helpero dd new-diff))]))

(defrel (div3o/even-odd n)
  (even-odd-helpero n 0))

(defrel (+1mod3o n n+1)
  (conde
    [(== n 0) (== n+1 1)]
    [(== n 1) (== n+1 2)]
    [(== n 2) (== n+1 0)]))
#|
</pre>

<pre>
> (run 30 (q) (div3o/even-odd q))
  ((())
   ((1 1))
   ((_0 _0 1 1))
   ((_0 _0 _1 _1 1 1))
   ((0 1 1))
   ((1 0 0 1))
   ((_0 _0 _1 _1 _2 _2 1 1))
   ((0 1 _0 _0 1))
   ((_0 _0 0 1 1))
   ((1 0 _0 _0 0 1))
   ((_0 _0 1 0 0 1))
   ((_0 _0 _1 _1 _2 _2 _3 _3 1 1))
   ((0 1 _0 _0 _1 _1 1))
   ((_0 _0 0 1 _1 _1 1))
   ((1 0 _0 _0 _1 _1 0 1))
   ((_0 _0 _1 _1 0 1 1))
   ((0 1 0 1 0 1))
   ((_0 _0 1 0 _1 _1 0 1))
   ((1 0 0 1 1 1))
   ((_0 _0 _1 _1 1 0 0 1))
   ((0 1 1 0 1 1))
   ((1 0 1 0 1))
   ((_0 _0 _1 _1 _2 _2 _3 _3 _4 _4 1 1))
   ((0 1 _0 _0 _1 _1 _2 _2 1))
   ((_0 _0 0 1 _1 _1 _2 _2 1))
   ((1 0 _0 _0 _1 _1 _2 _2 0 1))
   ((_0 _0 _1 _1 0 1 _2 _2 1))
   ((0 1 0 1 _0 _0 0 1))
   ((_0 _0 1 0 _1 _1 _2 _2 0 1))
   ((1 0 0 1 _0 _0 1 1)))
</pre>

<h2 id="riffleo">Riffling two lists together</h3>

<p>
Consider the way a dealer shuffles cards like in the below GIF. Two separate
decks get combined into one larger deck.
</p>

<img src="https://www.geogebra.org/resource/wbf3as26/nNXngeMdvMRfGZiL/material-wbf3as26.png">

<p>
In miniKanren, this would be a relationship between a list <code>l1</code>, a
list <code>l2</code>, and their riffled output <code>l1Ul2</code>. Here, the
<code>U</code> in <code>l1Ul2</code> is meant to look like multiset union.
</p>

<p>
We will write the miniKanren code as if it is checking to make sure the riffled
output is correct and follows logically from the inputs. First, let's check the
easy case: if one of <code>l1</code> or <code>l2</code> is empty, then the
riffled output is just the other list.
</p>

<pre>
(defrel (riffleo l1 l2 l1Ul2)
  (conde
    ;; If one of l1 or l2 is empty, then the riffled output is is equal to the
    ;; other list.
    ((== l1 '()) (== l1Ul2 l2))
    ((== l2 '()) (== l1Ul2 l1))

    ;; TODO: When both `a` and `b` are non-empty
    ))
</pre>

<p>
These cases are overlapping when both a and b are empty. It is good practice to
"expand out" the overlapping cases. So there should be three base cases: when
only <code>l</code> is empty, when only <code>r</code> is empty, and when they
are both empty.
</p>

<p>
As usual, we must positively express non-emptiness by asserting the existence
of a head and tail for the nonempty list.
</p>

<pre>
(defrel (riffleo l1 l2 l1Ul2)
  (fresh (a1 d1 a2 d2)
    (conde
      ;; If l1 and l2 are both empty, then the output is empty.
      ((== l1 '()) (== l2 '()) (== l1Ul2 '()))
      
      ;; If l1 is nonempty and l2 is empty, then the output is l1.
      ((== l1 `(,a1 . ,d1)) (== l2 '()) (== l1Ul2 l1))
      
      ;; If l1 is empty and l2 is nonempty, then the output is l2.
      ((== l1 '()) (== l2 `(,a2 . ,d2)) (== l1Ul2 l2))
      
      ;; TODO: when both l1 and l2 are nonempty
      )))
</pre>

<p>
The recursive case is when both <code>l1</code> and <code>l2</code> are
nonempty, meaning they both contain a head (<code>a1</code> and
<code>a2</code>, respectively) and a tail (<code>d1</code> and
<code>d2</code>). In the above GIF, either a left card falls into the output
deck, or the right card does. Either way, the riffle shuffle continues. So we
have two nonempty cases: either <code>a1</code> sits at the top of the deck
(meaning the rest of the deck is the result of riffling <code>d1</code> with
the intact <code>l2</code>), or vice versa: <code>a2</code> sits at the top of
the deck and the rest is <code>l1</code> riffled with <code>d2</code>.
</p>

<pre>
|#
(defrel (riffleo/v1 l1 l2 l1Ul2)
  (fresh (a1 d1 a2 d2 l1Ud2 d1Ul2)
    (conde
      ;; If l1 and l2 are both empty, then the output is empty.
      ((== l1 '()) (== l2 '()) (== l1Ul2 '()))
      
      ;; If l1 is nonempty and l2 is empty, then the output is l1.
      ((== l1 `(,a1 . ,d1)) (== l2 '()) (== l1Ul2 l1))
      
      ;; If l1 is empty and l2 is nonempty, then the output is l2.
      ((== l1 '()) (== l2 `(,a2 . ,d2)) (== l1Ul2 l2))
      
      ;; When both l1 and l2 are nonempty
      ((== l1 `(,a1 . ,d1)) (== l2 `(,a2 . ,d2))
       (conde
         ;; Either a1 is the first element in the riffled list
         ((== l1Ul2 `(,a1 . ,d1Ul2)) (riffleo/v1 d1 l2 d1Ul2))
         
         ;; Or, a2 is the first element in the riffled list
         ((== l1Ul2 `(,a2 . ,l1Ud2)) (riffleo/v1 l1 d2 l1Ud2)))))))
#|
</pre>

<p>
The above has the suffix <code>/v1</code> because, although the code is
complete, there is room for improvement.
</p>

<p>
In <code>riffleo/v1</code>, there are two recursive calls. By introducing fresh
variables, this recursive call can be factored out (or undistributed) from the
<code>conde</code>.
</p>

<pre>
|#
(defrel (riffleo/v2 l1 l2 l1Ul2)
  (fresh (a1 d1 a2 d2 l1Ud2 d1Ul2 x y xUy)
    (conde
      ;; If l1 and l2 are both empty, then the output is empty.
      ((== l1 '()) (== l2 '()) (== l1Ul2 '()))
      
      ;; If l1 is nonempty and l2 is empty, then the output is l1.
      ((== l1 `(,a1 . ,d1)) (== l2 '()) (== l1Ul2 l1))
      
      ;; If l1 is empty and l2 is nonempty, then the output is l2.
      ((== l1 '()) (== l2 `(,a2 . ,d2)) (== l1Ul2 l2))
      
      ;; When both l1 and l2 are nonempty
      ((== l1 `(,a1 . ,d1)) (== l2 `(,a2 . ,d2))
       (conde
         ;; Either a1 is the first element in the riffled list
         ((== l1Ul2 `(,a1 . ,d1Ul2)) (== `(,x ,y ,xUy) `(,d1 ,l2 ,d1Ul2)))
         
         ;; Or, a2 is the first element in the riffled list
         ((== l1Ul2 `(,a2 . ,l1Ud2)) (== `(,x ,y ,xUy) `(,l1 ,d2 ,l1Ud2))))

       ;; Either way, continue riffling
       (riffleo/v2 x y xUy)))))
#|
</pre>

<p>
Let this be the canonical implementation of riffling.
</p>

<pre>
|#
(defrel (riffleo l1 l2 l1Ul2)
  (riffleo/v2 l1 l2 l1Ul2))
#|
</pre>

<h3>Properties of riffling</h3>

<p>
Here are some useful observations around "riffling", asserted without proof.
Since <code>riffleo</code> can be thought of as a generalization of
<code>appendo</code>, it has many of the same properties.
</p>

<ol>
<li>
Like appending lists, riffling is <i>associative</i>. When three decks of cards
are riffled together, it does not make a difference which two decks are riffled
first.
</li>
<li>
Furthermore, riffling two lists together produces an output list whose length
equals the sum of the lengths of its input lists. In the same spirit, if some
element <code>a</code> comes before another element <code>b</code> in one of
the lists being riffled, then <code>a</code> must also come before
<code>b</code> in the output list. Riffling, in some sense, preserves the
internal order of both its input lists.
</li>
<li>
Unlike appending, riffling is <i>commutative</i>. That is,
<code>(riffleo a b c)</code> produces the same solution set as
<code>(riffleo b a c)</code>. When you riffle shuffle two decks together, does
it really matter which deck is in your left hand and which is in your right?
</li>
</ol>

<h3>Using <code>riffleo</code> to solve the 3-partition problem</h3>

<p>
Consider the NP-complete
<a href="https://en.wikipedia.org/wiki/3-partition_problem">
3-partition problem</a>, where the input is a list <code>l</code> of 3n numbers
and the output is a list <code>partitions</code> of n triples, such that each
partition adds up to the same <code>sum</code> and each element in each triple
is contained exactly once in <code>l</code>. In miniKanren, this problem can be
described as a relationship between input and output as follows:
</p>

<p>
When the input list <code>l</code> is empty, there are no partitions.
By vacuous truth, all the partitions add up to the same sum.
</p>

<p>
When <code>l</code> is nonempty, then it must have at least three elements that
add up to the <code>sum</code>, and the rest of the elements of <code>l</code>
must also form a 3-partition with the same <code>sum</code>. Crucially, the
three elements that add up to the <code>sum</code> (call them <code>e1</code>,
<code>e2</code>, and <code>e3</code>) can be picked, or "unriffled", from
<code>l</code>.
</p>

<pre>
|#

(defrel (3-partitiono l partitions sum)
  (conde
    ((== l '()) (== partitions '()))
    ((fresh (e1 e2 e3 e1+e2 rest-l rest-partitions)
       (== partitions `((,e1 ,e2 ,e3) . ,rest-partitions))
       (riffleo `(,e1 ,e2 ,e3) rest-l l)
       (pluso e1 e2 e1+e2)
       (pluso e1+e2 e3 sum)
       (3-partitiono rest-l rest-partitions sum)))))

#|
</pre>

<p>
Notice how this upholds the invariant that <code>l</code> is of some length 3n,
since three elements are picked from <code>l</code> recursively until it is
empty.
</p>

<p>
Let's see how this relation fares against
<a href="https://en.wikipedia.org/wiki/3-partition_problem#Example">
this first example from Wikipedia</a>. Of course, <code>pluso</code> only works
on Oleg numerals, so in order to convert the decimal inputs from the example,
<code>build-num</code> is used.
</p>

<pre>
> (run 1 (partitions sum)
    (3-partitiono
      (map build-num '(20 23 25 30 49 45 27 30 30 40 22 19))
      partitions
      sum))
> (run 1 (partitions sum)
      (3-partitiono
        (map build-num '(20 23 25 30 49 45 27 30 30 40 22 19))
        partitions
        sum))
((( ((0 0 1 0 1)   (1 0 0 1 1) (1 0 1 1 0 1))
    ((1 1 1 0 1)   (1 1 0 1 1) (0 0 0 1 0 1))
    ((0 1 1 1 1)   (0 1 1 1 1) (0 1 1 1 1)  )
    ((1 0 0 0 1 1) (0 1 1 0 1) (1 1 0 0 1)  ))
    
    (0 1 0 1 1 0 1)))
</pre>

<p>
Interpreting these results in decimal shows that the original list can be
partitioned into the triples <code>(20 25 45)</code>, <code>(23 27 40)</code>,
<code>(30 30 30)</code>, and <code>(49 22 19)</code>, each summing to 90.
</p>

<h2 id="3sat">3SAT</h2>
<p>
Another NP-complete problem is 3SAT: given a list of triples, where each
element in the triple represents a <i>literal</i>, that is, a number
representing the <b>i</b>th variable along with a boolean representing the
positive or negative version of the literal, does there exist a mapping of
each variable index to a boolean such that every triple contains at least one
correct assignment?
</p>

<p>
In order to encode 3SAT in miniKanren, we need a way to look up a variable's
value. A trie can help here, similar to a <a href="#sets-of-naturals">set of
Oleg numerals</a>.
</p>

<pre>
|#
(defrel (lookupo/trie map n val)
  (fresh (v l r)
    (== map `(,v ,l ,r))
    (conde
      ((== n '()) (== val v))
      ((fresh (a d rest-map)
         (== n `(,a . ,d))
         (conde
           ((== a 0) (poso d) (== rest-map l))
           ((== a 1) (== rest-map r)))
         (lookupo/trie rest-map d val))))))
#|
</pre>

<p>
A negative form of <code>lookupo/trie</code> is not needed for 3SAT, because
the mapping can remain open-ended for Oleg numerals that do not appear in the
formula.
</p>

<pre>
|#
(defrel (3sato dnf assignments)
  (conde
    ((== dnf '()))
    ((fresh (v1 v2 v3 p1 p2 p3 v p rest-dnf)
       (== dnf `((,p1 ,v1 ,p2 ,v2 ,p3 ,v3) . ,rest-dnf))
       (conde
         ((== p1 p) (== v1 v))
         ((== p2 p) (== v2 v))
         ((== p3 p) (== v3 v)))
       (lookupo/trie assignments v p)
       (3sato rest-dnf assignments)))))
#|
</pre>

<p>
Let's see if it can detect that the following formula is unsatisfiable.
</p>

<pre>
> (run 1 (q)
    (3sato '((#t () #t (1) #t (0 1))
             (#t () #t (1) #f (0 1))
             (#t () #f (1) #t (0 1))
             (#t () #f (1) #f (0 1))
             (#f () #t (1) #t (0 1))
             (#f () #t (1) #f (0 1))
             (#f () #f (1) #t (0 1))
             (#f () #f (1) #f (0 1))) q))

()
</pre>

<p>
It can! How about removing the last line? It should then be satisfiable, when
all three variables are assigned to <code>#t</code>.
</p>

<pre>
> (run 1 (q)
    (3sato '((#t () #t (1) #t (0 1))
             (#t () #t (1) #f (0 1))
             (#t () #f (1) #t (0 1))
             (#t () #f (1) #f (0 1))
             (#f () #t (1) #t (0 1))
             (#f () #t (1) #f (0 1))
             (#f () #f (1) #t (0 1))) q))

(((#t (_0 _1 (#t _2 _3)) (#t _4 _5))))
</pre>

<p>
That works too! In fact that should be the only answer.
</p>

<pre>
> (run* (q)
    (3sato '((#t () #t (1) #t (0 1))
             (#t () #t (1) #f (0 1))
             (#t () #f (1) #t (0 1))
             (#t () #f (1) #f (0 1))
             (#f () #t (1) #t (0 1))
             (#f () #t (1) #f (0 1))
             (#f () #f (1) #t (0 1))) q))

(((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5)))
 ((#t (_0 _1 (#t _2 _3)) (#t _4 _5))))
</pre>

<p>
Each identical answer here corresponds to an "execution path" the verifier
took--whether to satisfy using the first, second, or third literal for each
clause.
</p>


<h2 id="cartesian-product">The Cartesian product</h2>

<p>
The Cartesian product, typically defined over sets, can be written in
miniKanren over lists, similar to Python's
<a href="https://docs.python.org/3/library/itertools.html#itertools.product">
itertools.product</a>. Thus we can define a relation between three lists:
two "input" lists <code>l1</code> and <code>l2</code>, and a third list
<code>l1*l2</code> denoting their Cartesian product.
</p>

<p>
We recur over the first list <code>l1</code>. The base case is straightforward:
When <code>l1</code> is empty, then the product is empty. It is like
multiplying by zero.
</p>

<p>
The recursive case is more interesting. I always like to start by drawing a
picture and annotating things that should be named in green. This helps me
figure out the relationships.
</p>

<img src="img/cartesian-product.png">

|#
